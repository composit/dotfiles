# export this so child processes know how to edit things
export EDITOR='vim'

# some aliases
alias e=$EDITOR
alias sz='source ~/.zshrc'
alias ll='ls -lah'

# Vi mode (edit commands inline or in vim)
bindkey -v

# tm - create new tmux session, or switch to existing one. Works from within tmux too. (@bag-man)
# `tm` will allow you to select your tmux session via fzf.
# `tm irc` will attach to the irc session (if it exists), else it will create it.
tm() {
  [[ -n "$TMUX" ]] && change="switch-client" || change="attach-session"
  if [ $1 ]; then 
    tmux $change -t "$1" 2>/dev/null || (tmux new-session -d -s $1 && tmux $change -t "$1"); return
  fi
  session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --exit-0) &&  tmux $change -t "$session" || echo "No sessions found."
}

# auto completions
fpath=(~/.zsh/completion $fpath)
autoload -Uz compinit && compinit -i

# kubectl completion
source <(command kubectl completion zsh)

[[ -r ~/.zshrc.midnight ]] && . ~/.zshrc.midnight
[[ -r ~/.zshrc.local ]] && . ~/.zshrc.local

# fzf completions, etc
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# keep path unique by using zsh path and specifying uniqueness
# otherwise, loading tmux doubles the path
typeset -aU path

# Go stuff
path=(/usr/local/go/bin $path)
path=($HOME/go/bin $path)

# Ruby stuff
if [ -x "$(command -v ruby)" ]; then
  path=(`ruby -e 'puts Gem.user_dir'`/bin $path)
  alias rspec='nocorrect rspec'
fi

# Yarn stuff
if [ -x "$(command -v yarn)" ]; then
  path=(`yarn global bin` $path)
  path=("$HOME/.yarn/bin" "$HOME/.config/yarn/global/node_modules/.bin" $path)
fi

# Python stuff
if [[ -x "$(command -v python)" || -x "$(command -v python3)" ]]; then
  path=("$HOME/.local/bin" $path)
  path=("$HOME/.poetry/bin" $path)
fi

# ASDF (Elixir) stuff
if [ -d "$HOME/.asdf/asdf.sh" ]; then
  . $HOME/.asdf/asdf.sh
  . $HOME/.asdf/completions/asdf.bash
fi

# prompt
function git_branch_name()
{
  git_branch_name_msg=''
  branch=$(git symbolic-ref HEAD 2> /dev/null | awk 'BEGIN{FS="/"} {print $NF}')
  if [[ $branch != "" ]] then;
    git_branch_name_msg=' '$branch
  fi
}

# run git_branch_name before each prompt rendering
precmd() {
  git_branch_name
}

# allow for var substitutions in the prompt
setopt prompt_subst

PROMPT=''
PROMPT+='%F{247}' # gray
if [[ ! -z "$SSH_TTY" ]]; then # only display the hostname if not local
  PROMPT+='${(%):-%m} ' # %m is hostname
fi
PROMPT+='%B%3~%b' # %3~ is dir to 3 levels
PROMPT+='$git_branch_name_msg ' # git branch
PROMPT+='%f' # end gray color
PROMPT+='%(?.%F{230}☀%f.%F{174}☇%f) ' # exit code thingy

# don't add lines that start with a space into the history
HISTCONTROL=ignorespace

# git aliases
alias gs='git status'
alias gc='git commit'
alias ga='git add'
alias gd='git diff'
alias gb='git branch'
alias gp='git pull && git push'
